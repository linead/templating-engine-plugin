plugins {
  id 'org.jenkins-ci.jpi' version '0.27.0'
  id 'jacoco' 
  id 'application'
}

repositories {
    jcenter()
    maven {
        url('https://repo.jenkins-ci.org/public/')
    }
}

group = 'org.jenkins-ci.plugins'
version = '1.4' 
description = 'Allows users to create tool-agnostic, templated pipelines to be shared by multiple teams' 

jenkinsPlugin {
    coreVersion = '2.121.3'
    shortName = 'templating-engine'
    displayName = 'Templating Engine'
    url = 'https://wiki.jenkins.io/display/JENKINS/Templating+Engine+Plugin'
    disabledTestInjection = true
    localizerOutputDir = "${project.buildDir}/generated-src/localizer"
    configureRepositories = false
    configurePublishing = true
    developers {
        developer {
            id 'steven-terrana'
            name 'Steven Terrana'
            email 'terrana_steven@bah.com'
        }
        developer {
            id 'carlosokieffebah'
            name 'Carlos Okieffe' 
            email 'okieffe_carlos@bah.com' 
        }
    }
}

dependencies {    
    // plugin dependencies
    jenkinsPlugins "org.jenkins-ci.plugins.workflow:workflow-multibranch:2.20"
    jenkinsPlugins "org.jenkins-ci.plugins.workflow:workflow-api:2.28"
    jenkinsPlugins "org.jenkins-ci.plugins:branch-api:2.0.20"
    jenkinsPlugins "org.jenkins-ci.plugins:scm-api:2.2.7"
    jenkinsPlugins "org.jenkins-ci.plugins:junit:1.24"
    jenkinsPlugins "org.jenkins-ci.plugins:github-branch-source:2.5.1"

    // unit test deps 
    testCompile 'org.spockframework:spock-core:1.2-groovy-2.4'
    testCompile 'junit:junit:4.12'
    testCompile 'cglib:cglib-nodep:3.2.5'       // used by Spock
    testCompile 'org.objenesis:objenesis:2.5.1' // used by Spock

    // test plugins 
    jenkinsTest(group: "org.jenkins-ci.plugins", name: "scm-api", version: "2.3.0", classifier: "tests")
    jenkinsTest(group: 'org.jenkins-ci.plugins', name: 'git', version:'3.9.3') {
        exclude(module: 'httpclient')
        exclude(module: 'annotation-indexer')
    }
    testCompile(group: 'org.jenkins-ci.plugins', name: 'git', version:'3.9.3', classifier:'tests') {
        exclude(module: 'httpclient')
        exclude(module: 'annotation-indexer')
    }
    jenkinsTest "org.jenkins-ci.plugins.workflow:workflow-aggregator:2.6"
}

jacocoTestReport{
    reports{
        html.enabled true 
    }
}

task testStable(type: Test) {
    useJUnit {
        excludeCategories 'org.boozallen.plugins.jte.testcategories.Unstable'
        excludeCategories 'org.boozallen.plugins.jte.testcategories.InProgress'
    }
}

task testIntegration(type: Test) {
    useJUnit {
        includeCategories 'org.boozallen.plugins.jte.testcategories.Integration'
    }
}
